#!/usr/bin/perl
use warnings;
use strict;
use utf8;
use open OUT => ':encoding(UTF-8)', ':std';

use List::Util qw{ pairmap };

my %charmap = pairmap { chr oct $a => $b }
    203 => 'č',
    243 => 'ť',
    262 => 'ř',
    272 => 'ž',
    274 => 'Č',
    317 => 'ě',
    325 => 'ň',
    340 => 'ů',
    341 => 'á',
    376 => 'š';

my $match = join "", '[', keys %charmap, ']';

while (<>) {
    s/($match)/$charmap{$1}/g;
    print;
}

=head1 fixzip

When a zip created on MSWin contains Czech accented characters, the
encoding of the filenames is invalid. Run the filenames through this
script to fix them.

=cut
