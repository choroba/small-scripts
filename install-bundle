#!/usr/bin/perl
use warnings;
use strict;

my ($bundle_file) =  @ARGV;

open my $in, '<', $bundle_file or die $!;

$_ = <$in> until /^=head1 CONTENTS/ || eof;

my @install;
while (<$in>) {
    last if /^=head1/;

    my ($dist, $version) = split or next;

    print "$dist\r";
    if (0 == system $^X, '-M'.$dist, '-e1') {
        print ' ' x length $dist, "\r";
    } else {
        push @install, $dist;
        print "\n";
    }
}
close $in;

print "Going to install:\n";
print "@install\n";

exec 'cpan', @install if @install;

